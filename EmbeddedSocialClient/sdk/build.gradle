/*
 * Copyright (c) Microsoft Corporation. All rights reserved.
 * Licensed under the MIT License. See LICENSE in the project root for license information.
 */

buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
//apply plugin: 'com.google.gms.google-services' // applied at bottom of file to avoid dependency collisions

repositories {
    jcenter()
    maven {
        url "http://dl.bintray.com/acrowntest/test"
    }
}
android {
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    resourcePrefix 'es_'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        buildConfigField "com.microsoft.embeddedsocial.server.ServerConfig", "SERVER", "com.microsoft.embeddedsocial.server.ServerConfig.STAGING"
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "STANDALONE_APP", "false"
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "boolean", "STANDALONE_APP", "false"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debugStandalone {
            buildConfigField "boolean", "STANDALONE_APP", "true"
            signingConfig signingConfigs.debug
        }
        releaseStandalone {
            buildConfigField "boolean", "STANDALONE_APP", "true"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main {
            res.srcDirs 'src/main/res', 'src/main/res-public'
        }
    }
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = new File("EmbeddedSocial_SDK_$variant.buildType.name" + '.aar')
        }
    }
    productFlavors {
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api 'com.squareup.picasso:picasso:2.5.2'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.6.2'
    api 'com.google.android.gms:play-services-auth:16.0.1'
    api 'com.google.firebase:firebase-core:16.0.6'
    api "com.google.firebase:firebase-messaging:17.3.4"
    api 'net.openid:appauth:0.3.0'
    api 'com.squareup:otto:1.3.6'
    api 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    api 'com.squareup.okhttp3:okhttp:3.1.2'
    implementation ('androidx.work:work-runtime:2.0.0') {
        // workaround for build issue regarding com.google.common.util.concurrent.ListenableFuture
        //https://stackoverflow.com/questions/52467819/compile-errors-after-updating-to-workmanager-1-0-0-alpha09
        exclude group: 'com.google.guava', module: 'listenablefuture'
    }
    api 'androidx.appcompat:appcompat:1.0.0'
    api 'androidx.recyclerview:recyclerview:1.0.0'
    api 'androidx.cardview:cardview:1.0.0'
    api 'androidx.annotation:annotation:1.0.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'com.google.android.material:material:1.0.0'
    api 'de.hdodenhof:circleimageview:2.2.0'
    api 'com.facebook.android:facebook-android-sdk:4.39.0'
    api 'com.j256.ormlite:ormlite-android:4.48'
    api 'com.floreysoft:jmte:3.1.1'
    api 'com.github.chrisbanes.photoview:library:1.2.3'
    api 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    api 'com.acrowntest.test:autorest:0.7.3'
    api 'org.jetbrains:annotations:15.0'
    api 'com.microsoft.appcenter:appcenter-analytics:1.9.0'
}

ext {
    outputFolder = new File((File) project.getProjectDir(), "aar")
}

def clearOutputFolder() {
    File[] files = outputFolder.listFiles()
    if (files != null) {
        for (File file : files) {
            file.delete()
        }
    }
}

clearOutputFolder()


def siteUrl = 'https://github.com/Microsoft/EmbeddedSocial-Android-SDK'
def gitUrl = 'https://github.com/Microsoft/EmbeddedSocial-Android-SDK.git'
def versionNum = '0.7.5'
group = "com.acrowntest.test"
version = versionNum
def description = 'Embedded Social Android SDK version ' + versionNum

//task sourcesJar(type: Jar) {
//    from android.sourceSets.main.java.srcDirs
//    classifier = 'sources'
//}
//
//task javadoc(type: Javadoc) {
//    source = android.sourceSets.main.java.srcDirs
//    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
//    destinationDir = file("../javadoc")
//    failOnError false
//}
//
//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

//artifacts {
////    archives javadocJar
//    archives sourcesJar
//}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def pomConfig = {
    licenses {
        license {
            name 'MIT'
            url 'https://opensource.org/licenses/MIT'
        }
    }
    developers {
        developer {
            id 'acrown-msft'
            name 'Alex Crown'
            email 'acrown@microsoft.com'
        }
    }
    scm {
        connection gitUrl
        developerConnection gitUrl
        url siteUrl
    }
}

publishing {
    publications {
        sdk(MavenPublication) {
            artifact bundleReleaseAar

            pom.withXml {
                def root = asNode()
                def dependenciesNode = root.appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                root.appendNode('description', description)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    publications = ['sdk']
    pkg {
        repo = 'test'
        name = 'sdk'
        userOrg = 'acrowntest'
        licenses = ['MIT']
        vcsUrl = gitUrl
        publish = true
        version {
            name = versionNum
            desc = 'TestLib ' + versionNum
            released = new Date()
            vcsTag = versionNum
            attributes = ['gradle-plugin': 'com.acrowntest.test:sdk']
            gpg {
                sign = true
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
